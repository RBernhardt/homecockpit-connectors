subprojects {

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: 'signing'

    group = 'de.newsarea.homecockpit'
    version = '0.9.0-SNAPSHOT'

    configurations.all {
        // check for updates every build
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    test {
        useTestNG()
        options {
            useDefaultListeners = true
        }
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    signing {
        // only sign archives if keyId is set
        if(project.hasProperty("signing.keyId")
          && !project.getProperty("signing.keyId")?.isAllWhitespace()) {
            sign configurations.archives
        }

    }

    uploadArchives {
        repositories {
            mavenDeployer {

                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom {
                    project {
                        packaging 'jar'
                        url 'https://github.com/RBernhardt/homecockpit-connectors'

                        scm {
                            connection 'scm:git@github.com:RBernhardt/homecockpit-connectors.git'
                            developerConnection 'scm:git@github.com:RBernhardt/homecockpit-connectors.git'
                            url 'git@github.com:RBernhardt/homecockpit-connectors.git'
                        }

                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'bernhardt'
                                name 'Robert Bernhardt'
                            }
                        }
                    }
                }

            }
        }
    }

}

